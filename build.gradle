plugins {
    id "com.github.spotbugs" version "1.6.2"
    id "java-library"
    id "maven-publish"
    id "checkstyle"
    id "signing"
    id "jacoco"
}

group = "ninja.javahacker"
version = "0.4.0"
String artifactBaseName = "jpa-simple-transactions"

ext.javaCompiler = "1.9"
ext.doclint = false
ext.extraCompilerArgs = [
    "--add-modules", "java.xml.ws.annotation",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.comp=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.main=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.model=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.parser=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.processing=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED",
    "--add-opens", "jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED",
]

ext.versionApiguardian = "1.0.0"
ext.versionCheckstyle = "8.12"
ext.versionFindSecBugs = "1.8.0"
ext.versionJaCoCo = "0.8.1"
ext.versionJcip = "1.0"
ext.versionJpa = "2.2"
ext.versionJunit = "5.3.0-RC1"
ext.versionJsr305 = "3.0.2"
ext.versionLombok = "1.18.2" //"edge"
ext.versionPmd = "6.6.0"
ext.versionReifiedGenerics = version
ext.versionSpotBugs = "3.1.6"

dependencies {

    // JPA
    api group: 'javax.persistence', name: 'javax.persistence-api', version: "${versionJpa}"

    // JUnit
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "${versionJunit}"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "${versionJunit}"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: "${versionJunit}"
    testCompileOnly group: 'org.apiguardian', name: 'apiguardian-api', version: "${versionApiguardian}"

    // Lombok
    if (versionLombok != "edge") {
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${versionLombok}"
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "${versionLombok}"
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: "${versionLombok}"
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: "${versionLombok}"
    } else {
        annotationProcessor files("libs/lombok-edge.jar")
        compileOnly files("libs/lombok-edge.jar")
        testAnnotationProcessor files("libs/lombok-edge.jar")
        testCompileOnly files("libs/lombok-edge.jar")
    }

    // PMD
    compileOnly group: 'net.sourceforge.pmd', name: 'pmd', version: "${versionPmd}"
    testCompileOnly group: 'net.sourceforge.pmd', name: 'pmd', version: "${versionPmd}"

    // Reified-Generics
    api group: 'ninja.javahacker', name: 'reified-generic', version: "${versionReifiedGenerics}"

    // SpotBugs + Find Security Bugs
    compileOnly group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: "${versionFindSecBugs}"
    compileOnly group: 'net.jcip', name: 'jcip-annotations', version: "${versionJcip}"
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${versionSpotBugs}"
    compileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: "${versionJsr305}"
    testCompileOnly group: 'com.h3xstream.findsecbugs', name: 'findsecbugs-plugin', version: "${versionFindSecBugs}"
    testCompileOnly group: 'net.jcip', name: 'jcip-annotations', version: "${versionJcip}"
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: "${versionSpotBugs}"
    testCompileOnly group: 'com.google.code.findbugs', name: 'jsr305', version: "${versionJsr305}"
}

println "Using Java " + org.gradle.api.JavaVersion.current() + "."

repositories {
    flatDir {
        dirs 'libs'
    }
    mavenLocal()
    jcenter()
    mavenCentral()
}

tasks.withType(JavaCompile) {
    sourceCompatibility = javaCompiler
    targetCompatibility = javaCompiler
    options.encoding = 'UTF-8'
    options.debug = true
    options.fork = true
    options.compilerArgs << "-parameters" << "-Xlint:all,-processing"
    if (doclint) options.compilerArgs << "-Xdoclint:all"
    extraCompilerArgs.each { e -> options.compilerArgs << e }
}

tasks.withType(Javadoc) {
    options.encoding = "UTF-8"
    options.docEncoding = "UTF-8"
    options.addBooleanOption('html5', true)
    options.tags = ["apiNote:a:API Note:", "implSpec:a:Implementation Requirements:", "implNote:a:Implementation Note:"]
}

checkstyle {
    toolVersion = "${versionCheckstyle}"
    configFile = rootProject.file("config/checkstyle/checkstyle.xml")
    configProperties = [
        "checkstyle.cache.file": "${buildDir}/checkstyle.cache",
    ]
    ignoreFailures = true
    showViolations = true
}

spotbugs {
    toolVersion = "${versionSpotBugs}"
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jar {
    baseName = artifactBaseName
    duplicatesStrategy = "exclude"
    inputs.property("moduleName", "${group}.${baseName}")
    manifest {
        attributes("Automatic-Module-Name": "${group}.${baseName}")
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    classifier "javadoc"
}

test {
    useJUnitPlatform()
}

jacoco {
    toolVersion = "${versionJaCoCo}"
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId jar.baseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                developers {
                    developer {
                        name = 'Victor Williams Stafusa da Silva'
                        email = 'victorwssilva@gmail.com'
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}